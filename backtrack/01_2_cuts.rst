Отсечения
---------

Как правило, переборное решение, написанное тупо, делает кучу лишней
работы. А именно:

если нам надо найти объект, который удовлетворяет определённым
требованиям, то перебор находит ещё кучу объектов, которые этому
требованию не удовлетворяют (например, в задаче о разложении :math:`N`
на :math:`k` степеней двойки находятся куча наборов из :math:`k`
степеней двойки, которые в сумме не дают :math:`N`)

если надо найти объект с минимальной стоимостью и т.п., то находятся
куча объектов, у которых сумма явно не минимальная.

если надо посчитать некоторые объекты, то тоже возможно, что много веток
рекурсии не будут приводить к решению.

Очевидно, что, заставив перебор не делать хотя бы часть этой работы,
можно его резко ускорить. Т.е. мы хотим заставить перебор не перебирать
те ветки дерева решений, которые нам точно не интересны.

Разложение числа :math:`N` в сумму :math:`k` степеней двойки
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

У нас была такая процедура:

::

    procedure find(i:integer);
    var j:integer;
    begin
    if i>k then begin
       check;
       exit;
    end;
    for j:=0 to 30 do begin
        a[i]:=1 shl j;
        find(i+1);
    end;
    end;

Попробуем какие-нибудь ветки дерева решений поотсекать. Т.е. фактически
надо, чтобы :math:`find` не рассматривало некоторые варианты
:math:`a[i]`, которые нам точно не интересны.

Во-первых, очевидно, что не стоит рассматривать :math:`a[i]`, если оно
больше :math:`n`. Более того, если ввести глобальную переменную
:math:`s`, в которой хранить текущую сумму, то можно рассматривать
только :math:`a[i]`, которые не превосходят :math:`n-s`:

::

    var s:...
    procedure find(i:integer);
    var j:integer;
    begin
    if ....
    end;
    for j:=0 to 30 do if 1 shl j<=n-s then begin
        a[i]:=1 shl j;
        s:=s+a[i];
        find(i+1);
        s:=s-a[i]; {Откатываем назад!!!}
    end;
    end;

Очевидно, что это тоже будет работать корректно (кстати, это ещё и
исправляет баг с переполнением в процедуре :math:`check`, который
обсуждался в части I).

Заметьте, что можно это написать и по-другому:

::

    var s:...
    procedure find(i:integer);
    var j:integer;
    begin
    if i>k...
    end;
    if s>=n then
       exit;
    for j:=0 to 30 do  begin
        a[i]:=1 shl j;
        s:=s+a[i];
        find(i+1);
        s:=s-a[i]; {Откатываем назад!!!}
    end;
    end;

т.е. вынести проверку в начало процедуры :math:`find`. Теперь, если в
некоторый момент мы взяли :math:`a[i]>n-s`, то мы попробуем такой
вариант, войдём в :math:`find(i+1)`, но тут же выйдем назад, т.к.
:math:`s\geq n`. Обратите внимание, что проверка стала нестрогой
(:math:`\geq`, а не :math:`>`), т.к. я её переместил после проверки
``if i>k``.

(Этот вариант, в отличии от предыдущего, видимо, не будет решать бага с
переполнением, но баг с переполнением — это особая фича именно этой
задачи; в других задачах таких приколов может не быть).



.. task::

    Переделайте эту программу, чтобы не хранить глобальную
    переменную :math:`s`, а передавать :math:`s` (или :math:`n-s`, т.е.
    оставшееся число) через параметр процедуры. 
    |
    |
    Элементарно :)
    передаём :math:`s`
    
    ::
    
        procedure find(i:integer;s:integer);
        var j:integer;
        begin
        if ....
        end;
        if s>=n then
           exit;
        for j:=0 to 30 do  begin
            a[i]:=1 shl j;
            find(i+1,s+a[i]);
        end;
        end;
    
    соответственно в главной программе вызываем :math:`find(1,0)`;
    
    или передаём :math:`nn=n-s`
    
    ::
    
        procedure find(i:integer;nn:integer);
        var j:integer;
        begin
        if ....
        end;
        if nn<=0 then
           exit;
        for j:=0 to 30 do begin
            a[i]:=1 shl j;
            find(i+1,nn-a[i]);
        end;
        end;
    
    Обратите внимание, что здесь :math:`find` в главной программе вызываем
    :math:`find(1,n)`.
    |



Вообще мне кажется более естественным проводить отсечения в начале
процедуры :math:`find` (как правило, после проверки на выход из
рекурсии), как в последнем примере. Типа процедура :math:`find` сначала
оценит, стоит ли вообще возится с дальнейшим перебором: если не стоит (в
данном случае если :math:`s\geq n`), то :math:`exit`, иначе перебираем
все подряд,

Попробуем поотсекать дальше. Например, очевидно, что в начале
:math:`find(i)` у нас ещё :math:`k-i+1` мест не заполнены. На них
встанут как минимум единицы (т.е. :math:`2^0`), поэтому, если
:math:`s+k-i+1>n`, то тоже можно отсекать. Вообще, обычно отсечения
можно проводить почти что до бесконечности :) придумывая все более и
более точные критерии того, что решения не найдётся, и, если на
олимпиаде делать нечего, то можно над этим и думать. Главное, нигде не
наглючить, т.к. сложность программы с увеличением количества отсечений
возрастает, и соответственно возрастает вероятность ошибки, а вот
скорость работы программы может и не сильно увеличиваться.

Виды отсечений
^^^^^^^^^^^^^^

Какие обычно бывают отсечения:

Если задача программы — найти оптимальный объект (объект с минимальной
стоимостью и т.п.), то обычно можно оценить, какая минимальная стоимость
будет у объекта, когда мы его достроим, исходя из текущего начала
объекта (например, часто она больше текущей стоимости). Если эта «оценка
снизу» на стоимость достроенного объекта уже больше лучшей из полученных
на данный момент стоимостей, то дальше искать бессмысленно (см. пример
дальше).

Если задача программы — получить объект с определёнными свойствами, то
если очевидно, что это свойство точно уже не выполнить при данном
начале, то дальше искать бессмысленно (как в примере выше: если
:math:`s>n`, то искать дальше точно бессмысленно).

Если же задача программы — посчитать количество таких объектов, то здесь
все сложнее. Смысл отсечения тут разве что в том, чтобы каждая ветка
рекурсии заканчивалась нахождением решения. Пример будет ниже.

Сразу замечу про ещё одно важное отсечение: отсечение по времени. Про
него тоже скажу ниже.

Пример по второму пункту мы разобрали; разберём примеры по двум
оставшимся пунктам.

Пример на отсечения при подсчёте количества объектов
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

Пусть цель программы — посчитать объекты. Рассмотрим в качестве примера
задание [Perestroika\_simple] (сначала попробуйте сами его порешать!)
Конечно, как и предлагалось в подсказке, будем перебирать разбиения
нулей на группы. Будем решать задачу: сколькими способами можно разбить
:math:`m` нулей на :math:`nn` групп так, чтобы в последовательных
группах количества нулей или совпадали, или увеличивались на единицу.

Во-первых, тут :math:`i=1` — особый случай (см. ещё ниже). Когда мы
выбираем, сколько нулей у нас будет в первой группе, нет никаких
ограничений. А когда выбираем, сколько нулей в дальнейших группах, у нас
только два варианта: столько же, как и в прошлой группе, и на единицу
больше. Будем передавать в процедуру количество нулей, которые осталось
разбить по группам, чтобы было удобнее. Получаем следующий код
(дополнительные комментарии см. ниже):

::

    procedure find(i,k:integer); 
    var j:integer;
        prev:integer;
    begin
    if i>nn then begin
       check;
       exit;
    end;
    if k<=0 then 
       exit;
    if i=1 then begin
       for j:=1 to k do begin
           a[1]:=j;
           find(2,k-j);
       end;
    end else begin
        prev:=a[i-1]; 
        a[i]:=prev;
        find(i+1,k-a[i]);
        a[i]:=prev+1;
        find(i+1,k-a[i]);
    end;
    end;

Подумаем, как можно отсечь. По сути, наша цель — чтобы каждая ветка
заканчивалась нахождением решения. Подумаем, как может получиться так,
что решение не найдётся. Могут быть два варианта: либо у нас нулей
слишком мало осталось, либо слишком много. Что значит слишком мало —
значит, что, даже если расходовать их в группы по минимуму, нулей не
хватит. Групп у нас остаётся ещё :math:`nn-i+1`, в каждую надо как
минимум :math:`prev` нулей, поэтому, если :math:`k<prev\cdot(nn-i+1)`,
то решений точно не найдётся. Аналогично, что значит, что нулей слишком
много? В первую группу мы можем поставить максимум :math:`prev+1` нуль,
во вторую — :math:`prev+2` и т.д. В :math:`nn-i+1`-ую —
:math:`prev+nn-i+1`, тогда общее количество нулей (сумма арифметической
прогрессии) получится :math:`(prev+1+prev+nn-i+1)\cdot(nn-i+1)/2`.
Поэтому, если :math:`k>(prev+1+prev+nn-i+1)\cdot(nn-i+1)/2`, то решений
тоже точно не найдётся. Поэтому получаем отсечение

::

    if (k<prev*(nn-i+1))or (k>(prev+1+prev+nn-i+1)*(nn-i+1) div 2) then
       exit; 

и окончательное решение (привожу на этот раз полную программу):

::

    {$A+,B-,D+,E+,F-,G-,I+,L+,N+,O-,P-,Q+,R+,S+,T-,V-,X+,Y+}
    {$M 65520,0,655360}
    var a:array[1..100] of integer;
        n,m:integer;
        nn:integer;
        ans:longint;
        res:longint;

    procedure check;
    var i:integer;
        s:integer;
    begin
    s:=0;
    for i:=1 to nn do
        s:=s+a[i];
    if s<>m then 
       exit;
    for i:=2 to nn do 
        {на всякий случай проверим, что все правильно. 
        На самом деле это никогда не должно сработать}
        if (a[i]<>a[i-1])and(a[i]<>a[i-1]+1) then begin
           writeln('!!2');
           halt;
        end;
    inc(ans);
    end;

    procedure find(i,k:integer); 
          {k --- сколько нулей осталось разбить по группам}
    var j:integer;
        prev:integer;
    begin
    if i>nn then begin
       check;
       exit;
    end;
    if k<=0 then begin 
           {если нулей не осталось, 
            то бессмысленно что-либо перебирать. 
            Обратите внимание, что это 
            написано после предыдущего if'а.}
       exit;
    end;
    if i=1 then begin
          {i=1 здесь особый случай, т.к. у него 
          нет предыдущей группы. 
          Как это сделать поэлегантнее, я не придумал}
       for j:=1 to k do begin
           a[1]:=j;
           find(2,k-j);
       end;
    end else begin
        prev:=a[i-1]; 
        if (k<prev*(nn-i+1))or 
             (k>(prev+1+prev+nn-i+1)*(nn-i+1) div 2) then
           exit; 
        a[i]:=prev;
        find(i+1,k-a[i]);  {k-a[i] нулей осталось разбить}
        a[i]:=prev+1;
        find(i+1,k-a[i]);
    end;
    end;

    function count(n,m:integer):longint;
    begin
    ans:=0;
    nn:=n;
    if n>0 then
       find(1,m);
    count:=ans;
    end;

    begin
    read(n,m);
    res:=count(n-1,m)+2*count(n,m)+count(n+1,m);
    {если n=1, то в count передастся n-1=0. 
    Для этого и стоит проверка if n>0 в функции count}
    writeln(res);
    end.

Можете потестить это решение. На тесте, на котором самый большой ответ
при ограничениях :math:`n,m\leq 100` (видимо, :math:`n=27`,
:math:`m=100`) оно у меня работает секунды три, при том, что динамика
там же работает немногим быстрее. Если же отсечение убрать, то тормозит
много сильнее.

А теперь самое важное тут: обратите внимание, что теперь любая ветка
перебора (за исключением, возможно, небольшого их числа, у которых
отсечение произошло бы на последнем шаге) заканчивается нахождением
решения. Следовательно, мы можем оценить, сколько всего веток перебора
будет: у дерева решений листьев будет примерно столько же, сколько мы
найдём решений, т.е. равно ответу на задачу. Очевидно, что, поскольку мы
тут не умеем считать объекты пачками, т.е. мы каждый объект (разбиение)
считаем отдельно, то быстрее работать вряд ли получится: на каждый
объект нужен лист дерева решений, т.е. листьев должно быть не меньше,
чем ответ на задачу (с другой стороны то же самое: процедура
:math:`check` делает :math:`inc(ans)`, следовательно, она должны будет
быть вызвана как минимум столько раз, каков ответ на задачу. Могло
оказаться, что она вызвана будет намного большее количество раз, но в
нашей программе это не так: мы специально сделали, чтобы каждый вызов
:math:`check` делал :math:`inc(ans)`; ладно, почти каждый. Ещё меньше
вызовов :math:`check` сделать в рамках решений, которые делают только
:math:`inc(ans)`, невозможно).

Количество листьев приблизительно характеризует время работы программы:
понятно, что, чем их больше, тем дольше работает программа. Поэтому
всегда надо стараться уменьшить число листьев. Но тут мы уменьшили их до
минимума: меньше, чем ответ на задачу, не получится. Таким образом,
быстрее написать перебор, видимо, тут не получится. Мы оптимизировали
дерево решений как могли. (Не *программу*, а *дерево решений*. Решение,
может быть, можно оптимизировать ещё. Например, придумать, как убрать
цикл из процедуры check; может быть, избавиться от деления на 2 в
отсечении, и т.п. Но *дерево решений* все равно уже не изменится).

Кроме того, можно время, которое работает наша программа, теперь можно
оценить по ответу на тест (ведь процедура :math:`check` будет именно
столько раз вызываться — ну, почти столько); если ответ небольшой, то
можно рассчитывать, что наша программа тормозить не будет. Идея
переборного решения этой задачи родилась у меня, когда я узнал, что
максимальный ответ в тестах был пятизначным. Стало ясно, что такой
перебор тормозить не может.



.. task::

    (Творческое) Попробуйте придумать, как бы написать программу,
    чтобы не нужно было выделять :math:`i=1` в особый случай. Это не очень
    тривиально, и есть несколько вариантов, как это сделать. 
    |
    Я вижу как
    минимум два варианта; в обоих для вычисления ответа при данных
    :math:`nn` и :math:`m` придётся запускать процедуру :math:`find`
    несколько раз. Во-первых, можно в массиве :math:`a` устанавливать
    нулевой элемент, типа того: в процедуре :math:`count`:
    
    ::
    
        ans:=0;
        nn:=n;
        if n>0 then begin
           for i:=1 to nn do begin
               a[0]:=i;
               find(1,m);
           end;
        count:=ans;
    
    Можно передавать в процедуру :math:`find` параметр, указывающий
    предыдущее число; в :math:`count` опять потребуется цикл.
    
    В обоих случаях появляется ещё проблема с тем, что некоторые решения
    будут считаться дважды (решения, начинающиеся на 3, будут считаться при
    :math:`a[0]=2` и :math:`a[0]=3`). Можете подумать, что с этим делать.
    
    В общем, ответа на это задание я не привожу, если вы напишите
    что-нибудь, и оно будет правильно проходить тест 27 100 (на него ответ
    94762), то круты :)
    |
    |



Отсечения в задачах на оптимизацию
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

Пусть цель программы — найти оптимальный объект. Рассмотрим в качестве
примера задание [Numbers\_simple] про удаление чисел со штрафом.

(Ещё раз замечу, что на самом деле многие задачи, которые мы тут
обсуждаем, решаются более крутыми способами. Например, эта задача
решается динамикой. Но тут мы обсуждаем, как их можно было решать
перебором.)

Итак, наша программа будет перебирать все возможные последовательности
удаления чисел, и для каждой считать штраф. Процедура :math:`check`
будет проверять, верно ли, что штраф меньше оптимального, и, если да, то
запоминать текущее решение. Напишем программу следующим образом:

::

    var a,b,ans:array...
        nn:integer;
        cur,best:longint;

    procedure insert(i:integer;v:integer);
    var j:integer;
    begin
    for j:=nn downto i do
        b[j+1]:=b[j];
    b[i]:=v;
    inc(nn);
    end;

    function delete(i:integer):integer;
    var j:integer;
    begin
    delete:=b[i];
    for j:=i+1 to nn do
        b[i-1]:=b[i];
    dec(nn);
    end;

    procedure check;
    begin
    if cur<best then begin
       best:=cur;
       ans:=a;
    end;
    end;

    procedure find(i:integer);
    var j:integer;
        x:integer;
    begin
    if nn=2 then begin
       check;
       exit;
    end;
    for j:=2 to nn-1 do begin
        a[i]:=j;
        cur:=cur+b[j]*(b[j-1]+b[j+1]);
        x:=delete(j);
        find(i+1);
        insert(j,x);
        cur:=cur-b[j]*(b[j-1]+b[j+1]);
    end;
    end;

Поясню. У нас есть массив :math:`a`, в котором, как всегда, мы храним
текущее решение. В данном случае — последовательность номеров удаляемых
чисел. :math:`Ans` — наилучшее найденное на данный момент решение.
:math:`Cur` — текущий штраф (за те удаления, которые мы уже сделали),
:math:`best` — штраф в наилучшем найденном к данному моменту решении. В
массиве :math:`b` мы храним текущие числа, :math:`nn` — их количество.

Процедура :math:`delete` удаляет число из массива :math:`b`, корректируя
:math:`nn`, и возвращает удалённое число. Процедура :math:`insert`
отыгрывает удаление числа: вставляет его назад. На самом деле лучше было
бы работать со связными списками, где удалить и вставить число можно
намного быстрее (т.к. циклы в :math:`insert` и :math:`delete` сильно
тормозят программу), но, чтобы не загромождать основные идеи, в этом
примере я буду писать так. Процедура :math:`check` просто проверяет,
лучшее это решение, или нет.

Процедура :math:`find` — основная процедура перебора. Работает она так.
Во-первых, если в массиве осталось всего 2 элемента (:math:`nn=2`; на
самом деле, очевидно, это условие равносильно :math:`i=n-2`), то делать
больше ничего не надо (удалять надо все числа, кроме крайних), поэтому
:math:`check` и :math:`exit`.

В противном случае посмотрим, какое число будем удалять. Запомним его
номер в массиве :math:`a`, скорректируем текущий штраф :math:`cur` и
текущий массив :math:`b` (последнее — вызовом :math:`delete`), и пойдём
перебирать дальше: :math:`find(i+1)`. После этого не забудем вернуть все
назад!

Надеюсь, что вам понятно, как работает эта программа. Пара замечаний:

Здесь процесс «отката» изменений весьма нетривиален. Можно было
сохранить :math:`cur` и :math:`b` в стеке:

::

    procedure find(i:integer);
    var j:integer;
        ocur:... {old cur}
        ob:.. {old b}
    begin
    if nn=2 ...
    end;
    ocur:=cur;
    ob:=b;
    for j:=2 to nn-1 do begin
        a[i]:=j;
        cur:=cur+b[j]*(b[j-1]+b[j+1]);
        delete(j);
        find(i+1);
        b:=ob;
        cur:=ocur;
    end;
    end;

обратите внимание, что теперь переменная :math:`x` не нужна. Но в
результате может не хватить стека, т.к. каждая копия процедуры
:math:`find` будет хранить свой массив :math:`ob`.

На самом деле нас теперь массив :math:`a` нужен только для того, чтобы
выводить ответ. Если сам ответ выводить не надо, то можно не хранить
массив :math:`a` (и, соответственно, массив :math:`ans`) вообще. Тогда
нам не нужна будет и переменная :math:`i`; процедура :math:`find` теперь
не будет принимать никаких параметров (!), она теперь будет перебирать
один шаг удаления и запускаться рекурсивно для дальнейшего перебора (а
текущая глубина перебора пока неявно присутствует в переменной
:math:`nn`).



.. task::

    Напишите программу без массива :math:`a` и переменной
    :math:`i`. Попробуйте её осознать «с нуля». 
    |
    |
    Ну, собственно, все
    в тексте было сказано.
    
    ::
    
        procedure find;
        var j:integer;
            ocur:...
            ob:..
        begin
        if nn=2 then begin
           check;
           exit;
        end;
        ocur:=cur;
        ob:=b;
        for j:=2 to nn-1 do begin
            cur:=cur+b[j]*(b[j-1]+b[j+1]);
            delete(j);
            find;
            b:=ob;
            cur:=ocur;
        end;
        end;
    
    например (или через :math:`insert` и не хранить :math:`ob` и
    :math:`ocur`).
    
    Суть в осознании программы «с нуля». Действительно, что делает эта
    программа. Здесь процедура :math:`find` по заданной в массиве :math:`b`
    последовательности просто перебирает *все* варианты удаления этих чисел.
    Как она это делает? Если удалять нечего, то просто проверяем. Иначе
    перебираем, какое число будем удалять первым, удаляем его и вызовом
    :math:`find` переберём все варианты удаления оставшихся чисел. Процедура
    :math:`find` тут почти никак не учитывает предысторию, она просто
    смотрит на текущую позицию и думает, что бы с нею сделать…Может быть,
    это осознать даже проще, чем все мои рассуждения в I части про перебор
    окончаний решений и т.д.
    |





.. task::

    Напишите эту программу, работая со связными списками. 
    |
    На
    самом деле это задание не на перебор, а на работу со связными списками.
    Процедура :math:`find` останется той же, только теперь мы будем хранить
    текущую последовательность чисел не в массиве, а в списке (на массивах
    или в динамической памяти), т.к. так проще вставлять и удалять элементы.
    
    |
    Я предпочитаю хранить списки в динамической памяти; может быть, вам
    приятнее хранить их в массиве record’ов или в нескольких массивах.
    
    ::
    
        type pnode=^tnode;
             tnode=record prev,next:pnode; val:integer; end; 
             {предыдущий и следующий элементы и значение}
        var head:pnode {голова списка}
    
        procedure find(i:integer);
        var j:integer;
            ocur:...
            p:pnode;
        begin
        if nn=2 then begin
           check;
           exit;
        end;
        ocur:=cur;
        p:=head^.next; {начинаем со второго элемента в списке}
        j:=2;
        while p^.next<>nil do begin 
                {пока не дошли до конца списка; 
                обратите внимание, что последний 
                элемент списка не рассматриваем!}
            a[i]:=j;
            cur:=cur+p^.val*(p^.prev^.val+p^.next^.val);
            p^.prev^.next:=p^.next;
            p^.next^.prev:=p^.prev;  {удалили элемент p из списка}
            find(i+1);
            cur:=ocur;
            p^.prev^.next:=p;
            p^.next^.prev:=p;  {вставили его назад}
            p:=p^.next; {перешли к следующему}
            inc(j);
        end;
        end;
    
    Обратите внимание на то, что элемент удаляется из списка, но не из
    памяти. Его адрес остаётся в переменной :math:`p`, благодаря чему мы
    можем его восстановить назад (в этом смысле роль переменной :math:`p` в
    некотором смысле похоже на роль переменной :math:`x` в первоначальной
    версии реализации). Обычно вставка элемента в список делается немного
    хитрее, здесь же мы воспользовались тем, что этот элемент там только что
    был и мы его просто возвращаем на место.
    
    За счёт связных списков мы смогли избежать циклов в процедурах
    :math:`insert` и :math:`delete`, что должно сильно (порядка в :math:`N`
    раз) ускорить программу. Но, как уже говорилось, это не имеет отношения
    к перебору, а только к тому, что, если вы хотите вставлять/удалять
    элементы в произвольное место, то лучше использовать список, а не
    массив.
    
    Кроме того, обратите внимание на переменную :math:`j`. Ею мы просто
    считаем элементы списка, чтобы знать, что записать в массив :math:`a`.
    
    |



Подумаем тут над тем, какие можно придумать отсечения. Во-первых, если
все числа положительны, то очевидно, что если :math:`cur\geq best`, то
решения лучше чем текущее, мы точно не найдём. Поэтому первое отсечение
— ``if cur>=best then exit``. Это (как я уже говорил), фактически,
стандартное отсечение в подобных задачах.

Обратите внимание: условие нестрогое. ``if cur>=best``, а не
``if cur>best``. Действительно, нам несколько раз получать оптимальное
решение не надо. Вот если бы надо было вывести *все* оптимальные
решения, тогда пришлось бы писать :math:`cur>best`.

Можно пытаться придумывать другие отсечения. Например, за каждое
удаление мы получаем штраф, как минимум равный удвоенному удаляемому
числу (считаем, что у нас числа натуральные, и значит, сумма соседей
:math:`\geq 2`). Поэтому за удаление всех чисел мы получим штраф как
минимум равный удвоенной сумме всех чисел. Поэтому, если знать сумму
всех чисел (кроме крайних) :math:`s`, то можно отсекать по условию
:math:`cur+2\cdot s\geq best`. Сумму можно поддерживать во время работы
программы или каждый раз считать заново. (Поддерживать значит хранить в
отдельно переменной и быстро пересчитывать при каждом изменении массива.
Мы это уже много раз делали).



.. task::

    Напишите оба этих варианта программы: с хранением суммы в
    отдельной переменной или с вычислением каждый раз заново.
    
    |
    |
    Собственно, все просто.
    
    Если поддерживать сумму чисел:
    
    ::
    
        var s:...
        procedure find(i:integer);
        var j:integer;
            ocur:...
            ob:..
        begin
        if nn=2 then begin
           check;
           exit;
        end;
        if cur+2*s>=best then exit;
        ocur:=cur;
        ob:=b;
        for j:=2 to nn-1 do begin
            a[i]:=j;
            cur:=cur+b[j]*(b[j-1]+b[j+1]);
            s:=s-b[j];
            delete(j);
            find(i+1);
            b:=ob;
            cur:=ocur;
            s:=s+b[j];{Не забываем ее восстанавливать}
        end;
        end;
    
    Если вычислять каждый раз заново:
    
    ::
    
        procedure find(i:integer);
        var j:integer;
            ocur:...
            ob:..
            s:...
        begin
        if nn=2 then begin
           check;
           exit;
        end;
        s:=0;
        for j:=2 to nn-1 do
            s:=s+b[j];
        if cur+2*s>=best then exit;
        ocur:=cur;
        ob:=b;
        for j:=2 to nn-1 do begin
            a[i]:=j;
            cur:=cur+b[j]*(b[j-1]+b[j+1]);
            s:=s-b[j];
            delete(j);
            find(i+1);
            b:=ob;
            cur:=ocur;
            s:=s+b[j];{Не забываем ее восстанавливать, 
               т.к. она нам понадобится при следующем j}
        end;
        end;
    
    
    |



На самом деле, как я уже говорил, отсечения можно придумывать до
бесконечности. Можно учесть, какой минимальный элемент у нас остался, и
умножать не на 2, а на удвоенный этот элемент и т.д. Главное, не
запутаться в этих отсечениях, нигде не ошибиться, не опоздать решить
другую задачу :) и не писать отсечения, которые будут все равно
неэффективны. Т.е. главное — не переборщить.



.. task::

    Придумайте отсечения к задаче о паросочетании в произвольном
    графе (задание [matching], в обоих вариантах: а и б). Напишите полную
    программу. 
    |
    В а) проверяйте наличие ребра — и больше сложно что-то
    придумать; в б) можно написать стандартное отсечение для задач
    оптимизации: сравнить текущий ответ с наилучшим. Придумайте в б)
    что-нибудь ещё! Например, к текущей сумме можно добавлять вес
    минимального оставшегося ребра, умноженный на количество рёбер, которые
    ещё надо добавить. 
    |
    Я думаю, напишите, ничего тут сложного нет.
    |


